/* 
The InitTrig function runs only once, so there's no danger of creating a new map region for each unit created
I still don't know why globals don't work though

MUSTN'T FORGET:
  change MK's piercing bolt as it is the only trigger currently using unit custom values
  initialize udg_unit_array[]
*/


function Big_GroupAndIndex takes unit indexedUnit, boolean AddOrRemove returns nothing
	
  // if AddOrRemove is True, then the unit will be added to the group and will be given an index (LIFO?)
  // if AddOrRemove is False, then the unit will be removed from the group, but its Index will remain untouched
  
  local unit u = indexedUnit
  //local string temp_string
  
  if AddOrRemove == true then
    
	if not (GetUnitUserData(u) > 0) then
		set udg_index = udg_index + 1
		call SetUnitUserData (u, udg_index)
		set udg_unit_array[udg_index] = indexedUnit
	endif
    
    call GroupAddUnit (udg_Big_Group, u)
    
    /////////
	
	
	//set temp_string = GetUnitName(u) + " added to big group. Index " + I2S(udg_index)
    //call BJDebugMsg ( temp_string )
	
	
    /////////
  
  elseif GetUnitUserData(u) > 0 then //if false, duh
  
    // not removing the index, not only because it spares me a lot of trouble but I also think it is desirable in case a units is resurrected etc.
    call GroupRemoveUnit (udg_Big_Group, u)
    
    /////////
	
	
	//set temp_string = GetUnitName(u) + " removed from big group. Index " + I2S(GetUnitUserData(u))
    //call BJDebugMsg ( temp_string )
	
	
    /////////
    
  endif
  
  set u = null
endfunction

function RemovedUnit takes nothing returns boolean
  
  call Big_GroupAndIndex (GetTriggerUnit(), false)
  return false
  
endfunction

function CreatedUnit takes nothing returns boolean
  
  local unit temp = GetTriggerUnit()
  
  //checks if unit is not a dummy or similar
  if not (GetUnitAbilityLevel (temp, 'Aloc') > 0) and not (GetUnitUserData(temp) > 1) then
    call Big_GroupAndIndex (temp, true)
    
  endif
  
  set temp = null
  return false
endfunction

function PickUnitsOnInit takes nothing returns boolean

  local unit temp
  local group tempGroup = CreateGroup()
  local rect r = GetWorldBounds()
  
  call GroupEnumUnitsInRect (tempGroup, r, null)
  
  loop
    set temp = FirstOfGroup (tempGroup)
    exitwhen temp == null
    call Big_GroupAndIndex (temp, true)
    call GroupRemoveUnit (tempGroup, temp)
  endloop
  
  call DestroyGroup (tempGroup)
  set temp      = null
  set tempGroup = null
  
  return false
endfunction


//////////// TRIGGER ////////////////

function InitTrig_Big_Group takes nothing returns nothing

  
  local trigger existingUnits = CreateTrigger()
  local trigger newUnits      = CreateTrigger()
  local trigger removingUnits = CreateTrigger()
  
  local region rectRegion = CreateRegion()
  
  call RegionAddRect(rectRegion, bj_mapInitialPlayableArea)
  
  call TriggerRegisterTimerEvent      (existingUnits, 0.0, false)
  call TriggerAddCondition            (existingUnits, Condition  ( function PickUnitsOnInit ) )
  
  call TriggerRegisterEnterRegion	  (newUnits, rectRegion, null)
  
  call TriggerRegisterLeaveRegion	  (removingUnits, rectRegion, null)
  call TriggerRegisterAnyUnitEventBJ  (removingUnits, EVENT_PLAYER_UNIT_DEATH)
  
  call TriggerAddCondition            (newUnits,      Condition  ( function CreatedUnit ) )
  call TriggerAddCondition 	          (removingUnits, Condition  ( function RemovedUnit ) )
  
  set existingUnits  = null
  set newUnits = null
  set removingUnits = null
  set rectRegion = null
  
endfunction

///////////////////////

