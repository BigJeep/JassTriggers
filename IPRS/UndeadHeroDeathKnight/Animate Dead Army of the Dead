// A 40-second timer will go off once Animate Dead is cast, the same time as the animated dead duration, summoning random undead near the
// DK over the course of this timer
// At the end of it, if all Animated Dead survived, Animate Dead will be cast for free again if any corpses are nearby, and this will
// go on as long as each Animate Dead have animated dead surviving for the whole 40-second timer

// https://www.hiveworkshop.com/threads/request-unit-animation-backwards.266085/


globals
  integer array udg_dead_army_dk
  integer array udg_dead_army_alive
  timer   array udg_dead_army_timer
  boolean array udg_dead_army_bool
  boolean array udg_has_owning_dk
endglobals

// FUNCTIONS ===========================================================================================

function Army_of_the_Dead takes integer ownerId, integer dkId, real x, real y returns nothing

  local unit dk_dummy = CreateUnit (Player(ownerId), 'h00G', x, y, 0)
  set udg_dead_army_alive [dkId] = 0
  call BlzSetUnitName      (dk_dummy, I2S (dkId))
  call UnitApplyTimedLife  (dk_dummy, 'BTLF', 4     )
  call IssueImmediateOrder (dk_dummy, "animatedead" )

  call BJDebugMsg ( "Dummy Name: " + GetUnitName(dk_dummy))
  
  if udg_dead_army_timer [dkId] == null then
    set udg_dead_army_timer [dkId] = CreateTimer()
  endif

  call TimerStart (udg_dead_army_timer [dkId], 39, false, null) // must check if this works with an existing but expired timer
  
  set dk_dummy = null
endfunction


// ACTIONS =============================================================================================

function Animate_Dead_Death takes nothing returns nothing

  local unit dyingDead = GetTriggerUnit()

  local integer deadId  = GetUnitUserData (dyingDead)
  local integer ownerId = GetPlayerId (GetOwningPlayer(dyingDead))
  local integer dkId = udg_dead_army_dk [deadId]
  local unit dk = udg_unit_array [dkId]
  local real x = GetUnitX (dk)
  local real y = GetUnitY (dk)
  
  
  
  if TimerGetRemaining (udg_dead_army_timer [dkId]) > 0 then
    set udg_dead_army_bool [dkId] = false
	call BJDebugMsg ( "Reanimated died! " + I2S(udg_dead_army_alive [dkId]) + " reanimated remaining for DK " + I2S(dkId))
    // set udg_dead_army_alive [dkId] = 0 // udg_dead_army_alive [dkId] - 1
  elseif udg_dead_army_alive [dkId] > 1 then
	call BJDebugMsg ( "Reanimated died! " + I2S(udg_dead_army_alive [dkId]) + " reanimated remaining for DK " + I2S(dkId))
    set udg_dead_army_alive [dkId] = udg_dead_army_alive [dkId] - 1
  else
	call BJDebugMsg ( "All reanimated survived! repeating spell for DK " + I2S(dkId))
    call Army_of_the_Dead (ownerId, dkId, x, y)
  endif

  set dyingDead = null

endfunction


function Animate_Dead_Indexing takes nothing returns nothing // saves/stores the DK id for each summoned animated dead
  local unit anDead    = GetSummonedUnit()
  local integer deadId = GetUnitUserData (anDead)
  local integer dkId   = S2I (GetUnitName (GetSummoningUnit()))
  set udg_dead_army_dk  [deadId] = dkId
  set udg_has_owning_dk [deadId] = true
  set udg_dead_army_alive [dkId] = udg_dead_army_alive [dkId] + 1
  
  call BJDebugMsg ( "DK ID for new Animated Dead: " + I2S (dkId))
  call BJDebugMsg ( "ID Stored for " + GetUnitName(anDead) + ": "+ I2S (udg_dead_army_dk  [deadId]))
  
  set anDead = null
endfunction

function Animate_Dead_Create_Dummy takes nothing returns nothing // Creates the first dummy to cast AD, it is named after the DK id integer

  local unit caster = GetTriggerUnit()
  local integer casterId = GetUnitUserData (caster)
  local integer ownerId  = GetPlayerId (GetOwningPlayer(caster))
  local real x = GetUnitX(caster)
  local real y = GetUnitY(caster)

  call Army_of_the_Dead (ownerId, casterId, x, y)
	
	call BJDebugMsg ( "DK ID at cast: " + I2S (casterId))
	
  set caster = null

endfunction

// CONDITIONS ==========================================================================================

function Animate_Dead_dying_condition takes nothing returns boolean
  return udg_has_owning_dk [GetUnitUserData(GetTriggerUnit ())]
endfunction

function Animate_Dead_dummy_Condition takes nothing returns boolean
  return GetUnitTypeId(GetSummoningUnit()) == 'h00G'
endfunction

function Animate_Dead_cast_Condition takes nothing returns boolean
  return GetSpellAbilityId()  == 'AUan'
endfunction

// TRIGGER ============================================================================================

function InitTrig_Army_of_the_Dead takes nothing returns nothing

  local trigger cast = CreateTrigger()
  local trigger dummySpawn = CreateTrigger()
  local trigger death = CreateTrigger()

  call TriggerRegisterAnyUnitEventBJ (cast,       EVENT_PLAYER_UNIT_SPELL_EFFECT)
  call TriggerRegisterAnyUnitEventBJ (dummySpawn, EVENT_PLAYER_UNIT_SUMMON      )
  call TriggerRegisterAnyUnitEventBJ (death,       EVENT_PLAYER_UNIT_DEATH      )

  call TriggerAddCondition (cast,       Condition (function Animate_Dead_cast_Condition ))
  call TriggerAddCondition (dummySpawn, Condition (function Animate_Dead_dummy_Condition))
  call TriggerAddCondition (death,      Condition (function Animate_Dead_dying_condition))

  call TriggerAddAction    (cast,       function Animate_Dead_Create_Dummy)
  call TriggerAddAction    (dummySpawn, function Animate_Dead_Indexing    )
  call TriggerAddAction    (death,      function Animate_Dead_Death       )
  
  set cast  = null
  set death = null
  set dummySpawn = null

endfunction
